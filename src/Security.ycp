/* ------------------------------------------------------------------------------
 * Copyright (c) 2006-2012 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	modules/Security.ycp
 * Package:	Security configuration
 * Summary:	Data for the security configuration
 * Authors:	Michal Svec <msvec@suse.cz>
 *
 * $Id$
 */

{

module "Security";
textdomain "security";

import "FileUtils";
import "Package";
import "Pam";
import "Progress";
import "Service";

include "security/levels.ycp";


// services to check - these must be running
// meaning [ [ || ] && && ]
global const list<list<string> > mandatory_services = [["ntp"], ["syslog"], ["auditd"], ["random"], ["kbd"], ["cron"], ["postfix", "sendmail"]];
// sevices to check - these can be ignored (if they are running it's OK)
global const list<string> optional_services = [
    "acpid", "boot.clock", "dbus", "ealysyslog", "fbset", "framebufferset", "isdn", "microcode.ctl", "random",
    "consolekit", "haldaemon", "network", "syslog", "auditd", "splash_early", "alsasound",
    "irq_balancer", "kbd", "powersaved", "splash", "sshd", "earlyxdm", "hotkey-setup", "atd", "nscd",
    "smpppd", "xend", "autofs", "libvirtd", "sendmail", "postfix", "xendomains", "cron", "ddclient",
    "smartd", "stopblktrace", "ntp", "SuSEfirewall", "earlysyslog"
];
// All other services should be turned off

// systemd target, defining ctrl-alt-del behavior
const string ctrl_alt_del_file        =       "/etc/systemd/system/ctrl-alt-del.target";

// encryption methods supported by pam_unix (bnc#802006)
const list<string> encryption_methods     = [ "des", "md5", "sha256", "sha512" ];

// return list of missing mandatory services in a runlevel
global list<list<string> > MissingMandatoryServices(integer runlevel)
{
    y2milestone("Checking mandatory services in runlevel %1", runlevel);

    list<list<string> > ret = [];
    list<string> enabled_services = Service::EnabledServices(runlevel);

    y2milestone("enabled_services: %1", enabled_services);

    if (enabled_services == nil)
    {
	return nil;
    }

    foreach(list<string> services, mandatory_services,
	{
	    boolean enabled = false;

	    foreach(string service, services,
		{
		    enabled = enabled || contains(enabled_services, service);
		}
	    );

	    y2milestone("Mandatory services %1 are enabled: %2", services, enabled);

	    if (!enabled)
	    {
		ret = add(ret, services);
	    }
	}
    );

    y2milestone("Missing mandatory services in runlevel %1: %2", runlevel, ret);

    return ret;
}

global list<string> ExtraServices(integer runlevel)
{
    y2milestone("Searching for extra services in runlevel %1", runlevel);

    list<string> extra_services = [];
    list<string> enabled_services = Service::EnabledServices(runlevel);

    if (enabled_services == nil)
    {
	return nil;
    }

    foreach(string service, enabled_services,
	{
	    // the extra service is not mandatory and it's not optional
	    boolean extra = !contains(flatten(mandatory_services), service) && !contains(optional_services, service);

	    if (extra)
	    {
		y2milestone("Found extra service: %1", service);
		extra_services = add(extra_services, service);
	    }
	}
    );

    y2milestone("All extra services: %1", extra_services);

    return extra_services;
}

/**
 * All security settings
 */
global map<string,string> Settings = $[
    "CONSOLE_SHUTDOWN"			: "reboot",
    "CRACKLIB_DICT_PATH"		: "/usr/lib/cracklib_dict",
    "CWD_IN_ROOT_PATH"			: "yes",
    "CWD_IN_USER_PATH"			: "yes",
    "DISPLAYMANAGER_REMOTE_ACCESS"	: "no",
    "kernel.sysrq"			: "0",
    "net.ipv4.tcp_syncookies"		: "1",
    "net.ipv4.ip_forward"		: "0",
    "net.ipv6.conf.all.forwarding"	: "0",
    "FAIL_DELAY"			: "3",
    "GID_MAX"				: "60000",
    "GID_MIN"				: "1000",
    "DISPLAYMANAGER_SHUTDOWN"		: "all",
    "HIBERNATE_SYSTEM"			: "active_console",
    "LASTLOG_ENAB"			: "yes",
    "PASSWD_ENCRYPTION"			: "sha512",
    "GROUP_ENCRYPTION"			: "md5",
    "PASSWD_USE_CRACKLIB"		: "yes",
    "PASS_MAX_DAYS"			: "99999",
    "PASS_MIN_DAYS"			: "0",
    "PASS_MIN_LEN"			: "5",
    "PASS_WARN_AGE"			: "7",
    "PERMISSION_SECURITY"		: "secure",
    "DISABLE_RESTART_ON_UPDATE"		: "no",
    "DISABLE_STOP_ON_REMOVAL"		: "no",
    "RUN_UPDATEDB_AS"			: "nobody",
    "UID_MAX"				: "60000",
    "UID_MIN"				: "500",
    "SYS_UID_MAX"			: "499",
    "SYS_UID_MIN"			: "100",
    "SYS_GID_MAX"			: "499",
    "SYS_GID_MIN"			: "100",
    "USERADD_CMD"			: "/usr/sbin/useradd.local",
    "USERDEL_PRECMD"			: "/usr/sbin/userdel-pre.local",
    "USERDEL_POSTCMD"			: "/usr/sbin/userdel-post.local",
    "PASSWD_REMEMBER_HISTORY"		: "0",
    "SYSTOHC"				: "yes",
    "SYSLOG_ON_NO_ERROR"		: "yes",
    "DISPLAYMANAGER_ROOT_LOGIN_REMOTE"	: "no",
    "DISPLAYMANAGER_XSERVER_TCP_PORT_6000_OPEN" : "no",
    "SMTPD_LISTEN_REMOTE"		: "no",
    "RUNLEVEL3_MANDATORY_SERVICES"	: "yes",
    "RUNLEVEL5_MANDATORY_SERVICES"	: "yes",
    "RUNLEVEL3_EXTRA_SERVICES"		: "no",
    "RUNLEVEL5_EXTRA_SERVICES"		: "no",
];

// the original settings
map<string,string> Settings_bak = Settings;

// keys that should not be tested against predefined levels:
// - GROUP_ENCRYPTION does not have UI for changing
// - RUNLEVEL*_SERVICES have different syntax, are not saved in current form
global list<string> do_not_test = [ "GROUP_ENCRYPTION",
	"RUNLEVEL3_MANDATORY_SERVICES", "RUNLEVEL5_MANDATORY_SERVICES",
	"RUNLEVEL3_EXTRA_SERVICES", "RUNLEVEL5_EXTRA_SERVICES"
];

/**
 * Security settings locations
 */
map<string,list<string> > Locations = $[
    ".etc.login_defs" : [
	"FAIL_DELAY",
	"GID_MAX",
	"GID_MIN",
	"LASTLOG_ENAB",
	"PASS_MAX_DAYS",
	"PASS_MIN_DAYS",
	"PASS_WARN_AGE",
	"UID_MAX",
	"UID_MIN",
	"SYS_UID_MAX",
	"SYS_UID_MIN",
	"SYS_GID_MAX",
	"SYS_GID_MIN",
	"USERADD_CMD",
	"USERDEL_PRECMD",
	"USERDEL_POSTCMD",
    ],
    ".sysconfig.suseconfig" : [
	"CWD_IN_ROOT_PATH",
	"CWD_IN_USER_PATH",
    ],
    ".sysconfig.displaymanager" : [
	"DISPLAYMANAGER_REMOTE_ACCESS",
	"DISPLAYMANAGER_ROOT_LOGIN_REMOTE",
	"DISPLAYMANAGER_XSERVER_TCP_PORT_6000_OPEN",
	"DISPLAYMANAGER_SHUTDOWN",
    ],
    ".sysconfig.security" : [
	"PERMISSION_SECURITY",
    ],
    ".sysconfig.services" : [
	"DISABLE_RESTART_ON_UPDATE",
	"DISABLE_STOP_ON_REMOVAL",
    ],
    ".sysconfig.locate" : [
	"RUN_UPDATEDB_AS",
    ],
    ".sysconfig.clock" : [
	"SYSTOHC",
    ],
    ".sysconfig.cron" : [
	"SYSLOG_ON_NO_ERROR",
    ],
    ".sysconfig.mail" : [
	"SMTPD_LISTEN_REMOTE",
    ],
];

/**
 * Default values for /etc/sysctl.conf keys
 */
const map<string,string> sysctl	= $[
    "kernel.sysrq"		: "0",
    "net.ipv4.tcp_syncookies"	: "1",
    "net.ipv4.ip_forward"	: "0",
    "net.ipv6.conf.all.forwarding"	: "0"
];

/**
 * Mapping of /etc/sysctl.conf keys to old (obsoleted) sysconfig ones
 * (used during autoYaST import
 */
const map<string,string> sysctl2sysconfig	= $[
    "kernel.sysrq"		: "ENABLE_SYSRQ",
    "net.ipv4.tcp_syncookies"	: "IP_TCP_SYNCOOKIES",
    "net.ipv4.ip_forward"	: "IP_FORWARD",
    "net.ipv6.conf.all.forwarding"	: "IPV6_FORWARD"
];

/**
 * Mapping of /etc/login.defs keys to old (obsoleted) ones
 * (used during autoYaST import)
 */
const map<string,string> obsolete_login_defs	= $[
    "SYS_UID_MAX"       : "SYSTEM_UID_MAX",
    "SYS_UID_MIN"       : "SYSTEM_UID_MIN",
    "SYS_GID_MAX"       : "SYSTEM_GID_MAX",
    "SYS_GID_MIN"       : "SYSTEM_GID_MIN",
];

/**
 * mapping of internal YaST values to values needed for
 * org.freedesktop.upower.hibernate privilege
 */
const map<string,string> ycp2polkit	= $[
    "active_console"	: "auth_admin:auth_admin:yes",
    "auth_admin"	: "auth_admin:auth_admin:auth_admin",
    "anyone"		: "yes:yes:yes"
];

/*
 * Remaining settings:
 * - CONSOLE_SHUTDOWN (/etc/inittab)
 * - PASSWD_ENCRYPTION (/etc/pam?)
 * - RUNLEVEL3_MANDATORY_SERVICES
 * - RUNLEVEL5_MANDATORY_SERVICES
 * - RUNLEVEL3_EXTRA_SERVICES
 * - RUNLEVEL5_EXTRA_SERVICES
 */

/**
 * Number of sigificant characters in the password
 */
global map PasswordMaxLengths = $[
    "des" : 8,
    "md5" : 127,
    "sha256"	: 127,
    "sha512"	: 127
];

/**
 * Abort function
 * return boolean return true if abort
 */
global block<boolean> AbortFunction = nil;

/**
 * Check for pending Abort press
 * @return true if pending abort
 */
global define boolean PollAbort() {
    return UI::PollInput() == `abort;
}

/**
 * Abort function
 * @return blah blah lahjk
 */
global define boolean Abort() {
    if(AbortFunction != nil)
	return eval(AbortFunction) == true;
    return false;
}

/**
 * Data was modified?
 */
global boolean modified = false;

global boolean proposal_valid = false;
global boolean write_only = false;


/**
 * Function which returns if the settings were modified
 * @return boolean  settings were modified
 */
global define boolean GetModified () {
    return modified;
}
/**
 * Function sets internal variable, which indicates, that any
 * settings were modified, to "true"
 */


global define void SetModified () {
    modified = true;
}


/**
 * Data was modified?
 * @return true if modified
 */
global define boolean Modified() {
    y2debug("modified=%1", modified);
    return modified;
}

global void ReadServiceSettings()
{
    Settings["RUNLEVEL3_MANDATORY_SERVICES"] = MissingMandatoryServices(3) == [] ? "secure" : "insecure";
    Settings["RUNLEVEL5_MANDATORY_SERVICES"] = MissingMandatoryServices(5) == [] ? "secure" : "insecure";
    Settings["RUNLEVEL3_EXTRA_SERVICES"] = ExtraServices(3) == [] ? "secure" : "insecure";
    Settings["RUNLEVEL5_EXTRA_SERVICES"] = ExtraServices(5) == [] ? "secure" : "insecure";
}

/**
 * Read the information about ctrl+alt+del behavior
 * See bug 742783 for description
 */
global string ReadConsoleShutdown () {

    string ret  = "ignore";

    if (Package::Installed ("systemd"))
    {
        if (!FileUtils::Exists (ctrl_alt_del_file))
        {
            ret = "reboot";
        }
        else
        {
            string link = (string) SCR::Read (.target.symlink, ctrl_alt_del_file);
            if (link == "/lib/systemd/system/poweroff.target")
            {
                ret     = "halt";
            }
            else if (link == "/lib/systemd/system/reboot.target" ||
                     link == "/lib/systemd/system/ctrl-alt-del.target")
            {
                ret     = "reboot";
            }
        }
        return ret;
    }
    list inittab = SCR::Dir(.etc.inittab);
    if(contains(inittab, "ca")) {
	string ca = (string) SCR::Read(.etc.inittab.ca);
	if(issubstring(ca, "/bin/true") || issubstring(ca, "/bin/false"))
	    Settings["CONSOLE_SHUTDOWN"] = "ignore";
	else if(issubstring(ca, "reboot") || issubstring(ca, " -r"))
	    Settings["CONSOLE_SHUTDOWN"] = "reboot";
	else if(issubstring(ca, "halt") || issubstring(ca, " -h"))
	    Settings["CONSOLE_SHUTDOWN"] = "halt";
	else {
	    y2error("Unknown ca status: %1", ca);
	    Settings["CONSOLE_SHUTDOWN"] = "ignore";
	}
    }
    else
	Settings["CONSOLE_SHUTDOWN"] = "ignore";
}

/**
 * Read all security settings
 * @return true on success
 */
global define boolean Read() {

    Settings = $[];
    modified = false;

    /* Read security settings */

    mapmap(string file, list<string> vars, Locations, {
	maplist(string var, vars, {
	    string val = "";

	    string filename = nil;
	    if(issubstring(file, "sysconfig")) {
		filename = "/etc" + mergestring(splitstring(file, "."), "/");
		y2debug("filename=%1", filename);
	    }

	    if(filename == nil || SCR::Read(.target.size, filename) > 0) {
		val = (string) SCR::Read(topath(file + "." + var));
		y2debug("Reading: %1 (%2)", topath(file + "." + var), val);
	    }
	    if(val == nil) val = "";
	    Settings[var] = val;
	});
	return $[0: 0];
    });
    y2debug("Settings=%1", Settings);

    Settings["CONSOLE_SHUTDOWN"]        = ReadConsoleShutdown ();

    y2debug("Settings=%1", Settings);


    /* Read runlevel setting */
    ReadServiceSettings();

    /* Read pam settings */

    string method       = (string) SCR::Read (.etc.login_defs.ENCRYPT_METHOD);
    if (method == nil || !contains (encryption_methods, tolower (method)))
    {
        method    = "des";
    }
    Settings["PASSWD_ENCRYPTION"]	= method;

    // cracklib and pwhistory settings
    Settings ["PASS_MIN_LEN"] = "5";
    Settings ["PASSWD_USE_CRACKLIB"] = "no";
    Settings ["PASSWD_REMEMBER_HISTORY"]	= "0";

    map pam_cracklib	= Pam::Query ("cracklib");
    if (size (pam_cracklib) > 0)
	Settings ["PASSWD_USE_CRACKLIB"]	= "yes";
    // save the default value
    Settings ["CRACKLIB_DICT_PATH"] = "/usr/lib/cracklib_dict";
    foreach (string val, pam_cracklib["password"]:[], {
	list lval	= splitstring (val, "=");
	if (issubstring (val, "dictpath=")) {
            Settings ["CRACKLIB_DICT_PATH"] = lval[1]:"/usr/lib/cracklib_dict";
	}
	if (issubstring (val, "minlen=") && lval[1]:"" != "")
	    Settings ["PASS_MIN_LEN"] = lval[1]:"5";
    });

    map pam_history	= Pam::Query ("pwhistory");
    foreach (string val, pam_history["password"]:[], {
	list lval	= splitstring (val, "=");
	if (issubstring (val, "remember=") && lval[1]:"" != "")
	    Settings ["PASSWD_REMEMBER_HISTORY"] = lval[1]:"0";
    });

    y2debug("Settings=%1", Settings);

    /* Local permissions hack */

    string perm = Settings["PERMISSION_SECURITY"]:"";
    if(issubstring(perm, "easy")) perm = "easy";
    else if(issubstring(perm, "paranoid")) perm = "paranoid";
    else if(issubstring(perm, "secure")) perm = "secure";
    else perm = "secure";
    Settings["PERMISSION_SECURITY"] = perm;
    y2debug("Settings=%1", Settings);

    // read local polkit settings
    string action	= "org.freedesktop.upower.hibernate";
    string hibernate	= (string) SCR::Read (add (.etc.polkit-default-privs_local, action));
    if (hibernate != nil)
    {
	Settings["HIBERNATE_SYSTEM"]		= "active_console";
	if (hibernate == "auth_admin:auth_admin:auth_admin")
	    Settings["HIBERNATE_SYSTEM"]	= "auth_admin";
	if (hibernate == "yes:yes:yes")
	    Settings["HIBERNATE_SYSTEM"]	= "anyone";
    }
    y2debug ("HIBERNATE_SYSTEM: %1", Settings["HIBERNATE_SYSTEM"]:"");

    // read sysctl.conf
    foreach (string key, string default_value, sysctl, {
	string val	= (string) SCR::Read (add (.etc.sysctl_conf, key));
	if (val == nil || val == "")
	    val	= default_value;
	Settings[key]	= val;
    });
    y2debug ("Settings=%1", Settings);

    // remeber the read values
    Settings_bak = Settings;
    return true;
}


map<string,string> activation_mapping = $[
    "SYSLOG_ON_NO_ERROR" : "/etc/init.d/boot.clock start",
    "DHCPD_RUN_CHROOTED" : "/etc/init.d/dhcpd restart",
    "DHCPD_RUN_AS" : "/etc/init.d/dhcpd restart",
    // restart sendmail or postfix - whatever is installed
    "SMTPD_LISTEN_REMOTE" : "(test -e /etc/init.d/sendmail && VERBOSE=false /usr/lib/sendmail.d/update && /etc/init.d/sendmail restart) || (test -e /etc/init.d/postfix && /usr/sbin/SuSEconfig.postfix && /etc/init.d/postfix restart)",
    "net.ipv4.tcp_syncookies" : "/etc/init.d/boot.ipconfig start",
    "net.ipv4.ip_forward" : "/etc/init.d/boot.ipconfig start",
    "net.ipv6.conf.all.forwarding" : "/etc/init.d/boot.ipconfig start",
];

/**
 * Write the value of ctrl-alt-delete behavior
 */
boolean WriteConsoleShutdown (string ca) {

    if (Package::Installed ("systemd"))
    {
        if (ca == "reboot")
        {
            SCR::Execute (.target.remove, ctrl_alt_del_file);
        }
        else if (ca == "halt")
        {
            SCR::Execute (.target.bash,
                sformat ("ln -s -f /lib/systemd/system/poweroff.target %1",
                ctrl_alt_del_file));
        }
        else
        {
            SCR::Execute (.target.bash,
                sformat ("ln -s -f /dev/null %1", ctrl_alt_del_file));
        }
        return true;
    }

    if(ca == "reboot")
	SCR::Write(.etc.inittab.ca, ":ctrlaltdel:/sbin/shutdown -r -t 4 now");
    else if(ca == "halt")
	SCR::Write(.etc.inittab.ca, ":ctrlaltdel:/sbin/shutdown -h -t 4 now");
    else
	SCR::Write(.etc.inittab.ca, ":ctrlaltdel:/bin/true");
    SCR::Write(.etc.inittab, nil);

    // re-read the modified inittab (#83480)
    SCR::Execute (.target.bash, "/sbin/telinit q");
    return true;
}


/**
 * Write all security settings
 * @return true on success
 */
global define boolean Write() {

    if(!modified) return true;
    y2milestone("Writing configuration");

    /* Security read dialog caption */
    string caption = _("Saving Security Configuration");
    integer steps = 4;

    Progress::New( caption, " ", steps, [
	    /* Progress stage 1/4 */
	    _("Write security settings"),
	    /* Progress stage 2/4 */
	    _("Write inittab settings"),
	    /* Progress stage 3/4 */
	    _("Write PAM settings"),
	    /* Progress stage 4/4 */
	    _("Update system settings"),
	], [
	    /* Progress step 1/5 */
	    _("Writing security settings..."),
	    /* Progress step 2/5 */
	    _("Writing inittab settings..."),
	    /* Progress step 3/5 */
	    _("Writing PAM settings..."),
	    /* Progress step 4/5 */
	    _("Updating system settings..."),
	    /* Progress step 5/5 */
	    _("Finished")
	],
	""
    );

    /* Write security settings */
    if(Abort()) return false;
    Progress::NextStage();

    y2debug("Settings=%1", Settings);
    Settings["PERMISSION_SECURITY"] = Settings["PERMISSION_SECURITY"]:"" + " local";

    list<string> commitlist = [];
    mapmap(string file, list<string> vars, Locations, {
	maplist(string var, vars, {
	    string val = Settings[var]:"";
	    if(val == nil) val = "";
	    if(val != SCR::Read(topath(file + "." + var))) {
		SCR::Write(topath(file + "." + var), val);
		commitlist = (list<string>) union(commitlist, [ file ]);
	    }
	});
	return $[0: 0];
    });

    maplist(string file, commitlist, {
	SCR::Write(topath(file), nil);
    });

    /* Write inittab settings */
    if(Abort()) return false;
    Progress::NextStage();

    WriteConsoleShutdown (Settings["CONSOLE_SHUTDOWN"]:"ignore");

    /* Write pam settings */
    if(Abort()) return false;
    Progress::NextStage();

    /* pam stuff */
    string encr = Settings["PASSWD_ENCRYPTION"]:"sha512";
    if (encr != Settings_bak["PASSWD_ENCRYPTION"]:"")
    {
      SCR::Write (.etc.login_defs.ENCRYPT_METHOD, encr);
    }

    // use cracklib?
    if(Settings["PASSWD_USE_CRACKLIB"]:"no" == "yes") {
	Pam::Add ("cracklib");
	string pth = Settings["CRACKLIB_DICT_PATH"]:"/usr/lib/cracklib_dict";
	if (pth != "/usr/lib/cracklib_dict")
	    Pam::Add ("--cracklib-dictpath=" + pth);
    }
    else
	Pam::Remove ("cracklib");

    // save min pass length
    if (Settings["PASS_MIN_LEN"]:"5" != "5" && Settings["PASSWD_USE_CRACKLIB"]:"no" == "yes")
    {
	Pam::Add ("cracklib"); // minlen is part of cracklib
	Pam::Add (sformat ("cracklib-minlen=%1", Settings["PASS_MIN_LEN"]:"5"));
    }
    else
	Pam::Remove ("cracklib-minlen");

    // save "remember" value (number of old user passwords to not allow)
    if (Settings["PASSWD_REMEMBER_HISTORY"]:"0" != "0")
    {
	Pam::Add ("pwhistory");
	Pam::Add (sformat ("pwhistory-remember=%1",
	    Settings["PASSWD_REMEMBER_HISTORY"]:"0"));
    }
    else
	Pam::Remove ("pwhistory-remember");

    // write local polkit settings
    if (Settings["HIBERNATE_SYSTEM"]:"" != Settings_bak["HIBERNATE_SYSTEM"]:"")
    {
	// allow writing any value (different from predefined ones)
	string ycp_value= Settings["HIBERNATE_SYSTEM"]:"active_console";
	string hibernate= ycp2polkit[ycp_value]:ycp_value;
	string action	= "org.freedesktop.upower.hibernate";
	SCR::Write (add (.etc.polkit-default-privs_local, action), hibernate);
    }

    // write sysctl.conf
    foreach (string key, string default_value, sysctl, {
	string val	= Settings[key]:default_value;
	if (tointeger (val) == nil)
	{
	    y2error ("value %1 for %2 is not integer, not writing", val, key);
	}
	else if (val != SCR::Read (add (.etc.sysctl_conf, key)))
	{
	    SCR::Write (add (.etc.sysctl_conf, key), val);
	}
    });

    /* enable sysrq? */
    integer sysrq = tointeger (Settings["kernel.sysrq"]:"0");
    if(sysrq != nil)
    {
	SCR::Execute (.target.bash, sformat ("echo %1 > /proc/sys/kernel/sysrq", sysrq));
    }

    // Finish him
    if(Abort()) return false;
    Progress::NextStage();

    // apply all current permissions as they are now (what SuSEconfig --module permissions would have done)
    SCR::Execute(.target.bash, "/usr/bin/chkstat --system");

    // ensure polkit privileges are applied (bnc #541393)
    if (FileUtils::Exists ("/sbin/set_polkit_default_privs"))
	SCR::Execute (.target.bash, "/sbin/set_polkit_default_privs");

    if(Abort()) return false;
    Progress::NextStage();

    // activate the changes
    foreach(string setting, string action, activation_mapping,
	{
	    if (Settings[setting]:"" != Settings_bak[setting]:"")
	    {
		y2milestone("Option %1 has been modified, activating the change: %2", setting, action);

		integer res = (integer)SCR::Execute(.target.bash, action);
		if (res != 0)
		{
		    y2error("Activation failed");
		}
	    }
	}
    );

    if(Abort()) return false;
    modified = false;
    return true;
}

/**
 * Get all security settings from the first parameter
 * (For use by autoinstallation.)
 * @param settings The YCP structure to be imported.
 * @return boolean True on success
 */
global define boolean Import(map settings) {
    if(settings == $[]) return true;

    modified = true;
    map tmpSettings = $[];
    foreach(string k, string v, Settings, {
	if (!haskey(settings, k))
	{
	    if (haskey (sysctl, k) && haskey (settings, sysctl2sysconfig[k]:""))
	    {
		string val	= settings[sysctl2sysconfig[k]:""]:"";
		if (val == "yes")
		    tmpSettings[k] = "1";
		else if (val == "no")
		    tmpSettings[k] = "0";
		else
		    tmpSettings[k] = val;
	    }
            else if (haskey (settings, obsolete_login_defs[k]:""))
            {
                tmpSettings[k]  = settings[obsolete_login_defs[k]:""]:"";
            }
	    else
	    {
		tmpSettings[k] = v;
	    }
	}
	else
	{
	    tmpSettings[k] = settings[k]:"";
	}
    });
    Settings = (map<string,string>) eval(tmpSettings);
    return true;
}

/**
 * Dump the security settings to a single map
 * (For use by autoinstallation.)
 * @return map Dumped settings (later acceptable by Import ())
 */
global define map Export() {
    return (map) eval(Settings);
}

/**
 * Create a textual summary and a list of unconfigured cards
 * @return summary of the current configuration
 */
global define list Summary() {

    map<string,string> settings = Security::Settings;
    foreach (string key, Security::do_not_test, {
        settings        = remove (settings, key);
    });

    /* Determine current settings */
    any current = `custom;
    maplist(string key, map level, Levels, {
	y2debug("%1=%2", key, level);
	if(level == settings)
	    current = key;
    });
    y2debug("%1=%2", current, Settings);

    /* Summary text */
    string summary = _("Current Security Level: Custom settings");
    if(current != `custom)
	/* Summary text */
	summary = sformat(_("Current Security Level: %1"), LevelsNames[(string) current]:"");

    return [ summary, [] ];
}

/**
 * Create an overview table with all configured cards
 * @return table items
 */
global define list Overview() {
    return [];
}

/* EOF */
}
